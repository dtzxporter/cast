 global proc int castTranslatorOptions(string $parent, string $action, string $settings, string $callback) {
    global string $exportModel, $exportAnim, $bakeKeyframes;

    if ($action == "post") {
        setParent $parent;

        text -l "Model Settings" -align "left" -font "boldLabelFont" -height 30;

        $exportModel = `checkBox
            -l "Export Models"
            -align "left"
            -v false
            -w 150`;

        text -l "Animation Settings" -align "left" -font "boldLabelFont" -height 30;

        $exportAnim = `checkBox
            -l "Export Animations"
            -align "left"
            -v false
            -w 150`;

        $bakeKeyframes = `checkBox
            -l "Bake Keyframes"
            -align "left"
            -v false
            -w 150`;

        string $options[];

        int $numOptions = `tokenize $settings ";" $options`;

        for ($i = 0; $i < $numOptions; $i++) {
            string $option[];

            int $numParts = `tokenize $options[$i] "=" $option`;

            if ($numParts < 2)
                continue;

            if ($option[0] == "exportModel") {
                if ($option[1] == "1") {
                    checkBox -e -v 1 $exportModel;
                }
            } else if ($option[0] == "exportAnim") {
                if ($option[1] == "1") {
                    checkBox -e -v 1 $exportAnim;
                }
            } else if ($option[0] == "bakeKeyframes") {
                if ($option[1] == "1") {
                    checkBox -e -v 1 $bakeKeyframes;
                }
            }
        }

        return 1;
    } else if ($action == "query") {
        string $options = "exportModel=";

        if (`checkBox -query -value $exportModel` == 1)
            $options += "1;";
        else
            $options += "0;";

        $options += "exportAnim=";

        if (`checkBox -query -value $exportAnim` == 1)
            $options += "1;";
        else
            $options += "0;";

        $options += "bakeKeyframes=";

        if (`checkBox -query -value $bakeKeyframes` == 1)
            $options += "1;";
        else
            $options += "0;";

        eval($callback + "\"" + $options + "\"");

        return 1;
    }

    return 0;
}